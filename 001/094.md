Genuine Fiery Cheetah

Medium

# Preventing KWENTA tokens to be recovered can lead to significant losses

## Summary
This issue can lead to leftover `KWENTA` rewards being locked in the contract due to a potential discrepancy between `lastUpdateTime` and `rewardPerTokenStored`. It will occur when the total supply goes to zero and then a new user stakes, causing a loss of tokens.

## Vulnerability Detail
The `recoverERC20` function does not allow for recovering `KWENTA` tokens:

[StakingRewardsV2.sol#L710-L714](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L710-L714)
```solidity
    function recoverERC20(address _tokenAddress, uint256 _tokenAmount) external onlyOwner {
        if (_tokenAddress == address(kwenta)) revert CannotRecoverStakingToken();
        emit Recovered(_tokenAddress, _tokenAmount);
        IERC20(_tokenAddress).transfer(owner(), _tokenAmount);
    }
```

In most cases, it would not be a huge issue because it's very unlikely that anyone will send `KWENTA` tokens directly to the contract. But there is a scenario for which it's possible to have `KWENTA` reward tokens leftover in the contract without having them re-used in the next reward cycle when calling `notifyRewardAmount`. The way `notifyRewardAmount` recycles leftover rewards and adds them to the new rewards is by using the remaining amount of the last period, multiplying it by the `rewardRate` and using the `leftover` amount to calculate the new `rewardRate`:

[StakingRewardsV2.sol#L645-L666](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L645-L666)
```solidity
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;


            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;


            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }


        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```

The problem with this approach is that if the rewards are not always linearly distributed with the passage of time as defined by the `rewardRate` during the defined time period, it will lead to a discrepancy.

[StakingRewardsV2.sol#L410-L427](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L410-L427)
```solidity
    function _updateReward(address _account) internal {
        rewardPerTokenStored = rewardPerToken();
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC();
        lastUpdateTime = lastTimeRewardApplicable();


        if (_account != address(0)) {
            // update amount of rewards a user can claim
            rewards[_account] = earned(_account);


            // update reward per token staked AT this given time
            // (i.e. when this user is interacting with StakingRewards)
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;


            rewardsUSDC[_account] = earnedUSDC(_account);


            userRewardPerTokenPaidUSDC[_account] = rewardPerTokenStoredUSDC;
        }
    }
```
When does this happen? It happens when `lastUpdateTime` is updated but `rewardPerTokenStored` is not. This is possible when the total supply goes back to `0` and a new user stakes. If all tokens are unstaked and a new user is the first to stake again, the `lastUpdateTime` will get updated but `rewardPerTokenStored` will not, creating a (non-dust) discrepancy between the real amount of token left in the contract and the result of `remaining * rewardRate` in `notifyRewardAmount`.

[StakingRewardsV2.sol#L435-L444](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L435-L444)
```solidity
    function rewardPerToken() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();


        if (allTokensStaked == 0) {
            return rewardPerTokenStored;
        }


        return rewardPerTokenStored
            + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / allTokensStaked);
    }
```

The next time `notifyRewardAmount` is called, it will not handle the leftover rewards correctly and will not roll all of them to the next period. The reward tokens corresponding to the period nobody was staking will be left in the contract.

## Impact
I would consider the inability to recover `KWENTA` tokens alone a low severity issue. But as I demonstrated, it's possible for the rewards to be distributed non-linearly (`lastUpdateTime` is updated but `rewardPerTokenStored` is not), leading to a non negligible loss of tokens locked in the contract (high impact). Given that, there needs to be a window where nobody is staking anymore, it's fairly unlikely to happen (low likelihood). This is why I believe this issue to be of medium severity.


## Code Snippet
[StakingRewardsV2.sol#L710-L714](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L710-L714)
[StakingRewardsV2.sol#L645-L666](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L645-L666)
[StakingRewardsV2.sol#L410-L427](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L410-L427)
[StakingRewardsV2.sol#L435-L444](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L435-L444)

## Tool used

Manual Review.

## Recommendation
I would recommend allowing the recovery of `KWENTA` tokens.