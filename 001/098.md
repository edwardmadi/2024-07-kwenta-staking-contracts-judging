Plain Marigold Barbel

High

# Rewards lost due to delay between new reward period and first staker.

## Summary
A portion of the rewards or even all rewards (depending on delay) will be lost inside the `StakingRewardsV2` contract if there aren't any stakers at the time when `notifyRewardAmount()` is called for a new reward period.

## Vulnerability Detail
When `notifyRewardAmount()` is called, it sets `periodFinish = block.timestamp + rewardsDuration`, meaning that the rewards will be distributed within that time frame. However, if there are no stakers at the time of this call and the first staker stakes after some delay (Y), there will be a gap during which no rewards are distributed, resulting in lost rewards. `periodFinish` does not adjust to account for this delay.

For example, if `Y` is the delay before the first staker stakes:
1. `notifyRewardAmount()` is called at timestamp `X`.
2. `periodFinish` is set to `X + rewardsDuration`.
3. The first staker stakes at timestamp `X + Y`.

At this point, the clock has started for the staker and rewards will start accumulating from `X + Y`, but the `periodFinish` remains unchanged at `X + rewardsDuration`. This results in only `rewardsDuration - Y` seconds of rewards being distributed, leaving `Y * rewardRate` tokens locked within the contract, as they were not distributed within the intended reward period.

## Impact
Lost of rewards/tokens for the users and protocol. 

The amount is based on how big the delay is between the start of the rewards period and the first staker. The loss can be calculated with the formula: `loss = Y * rewardRate`, where Y is the delay/time in seconds when there are no stakers. Additionally, there is a potential but unlikely scenario where `Y == rewardsDuration`, meaning that there weren't any stakers in the reward period at all and all rewards will be lost.

## Code Snippet
`periodFinish` is set when `notifyRewardAmount()` is called:
```solidity
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        ...

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L645-L666

When the first staker stakes the clock for the staker would start at that block timestamp:
```solidity
    function lastTimeRewardApplicable() public view returns (uint256) {
        return block.timestamp < periodFinish ? block.timestamp : periodFinish;
    }

```
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L459-L461

## Tool used
Manual Review

## Recommendation
Starting the period/setting `periodFinish` when the first staker is about to stake trough `stake()` or `stakeEscrow()`.
```diff
+    uint256 public rewardsReady;
+    uint256 public rewardsUSDCReady;

    function stake(uint256 _amount) external whenNotPaused updateReward(msg.sender) {
        if (_amount == 0) revert AmountZero();
+        _startRewardPeriodIfNecessary()

        ...
        // Rest of the code
    }

    function _stakeEscrow(address _account, uint256 _amount)
        internal
        whenNotPaused
        updateReward(_account)
    {
        if (_amount == 0) revert AmountZero();
        uint256 unstakedEscrow = unstakedEscrowedBalanceOf(_account);
        if (_amount > unstakedEscrow) revert InsufficientUnstakedEscrow(unstakedEscrow);
+        _startRewardPeriodIfNecessary()

        ...
        // Rest of the code
    }

+    function _startRewardPeriodIfNecessary() internal {
+        if (totalSupply() == 0 && (rewardsReady > 0 || rewardsUSDCReady > 0)) {
+            _startRewardPeriod(rewardsReady, rewardsUSDCReady);
+            rewardsReady = 0;
+            rewardsUSDCReady = 0;
+    
+            // Need to run updateRewards again
+            _updateReward(msg.sender);
+        }
+    }

    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
-        if (block.timestamp >= periodFinish) {
-            rewardRate = _reward / rewardsDuration;
-            rewardRateUSDC = _rewardUsdc / rewardsDuration;
-        } else {
-            uint256 remaining = periodFinish - block.timestamp;
-
-            uint256 leftover = remaining * rewardRate;
-            rewardRate = (_reward + leftover) / rewardsDuration;
-
-            uint256 leftoverUsdc = remaining * rewardRateUSDC;
-            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
-        }

+        if (block.timestamp < periodFinish) {
+            uint256 remaining = periodFinish - block.timestamp;
+
+            uint256 leftover = remaining * rewardRate;
+            _reward += leftover;
+
+            uint256 leftoverUsdc = remaining * rewardRateUSDC;
+            _rewardUsdc += leftoverUsdc;
+        }
+
+        if (totalSupply() == 0) {
+            // No deposits yet, so keep rewards pending until first deposit
+            // Incrementing in case it is called twice
+            rewardsReady += _reward;
+            rewardsUSDCReady += _rewardUsdc;
+        } else {
+            // Ready to start
+            _startRewardPeriod(_reward, _rewardUsdc);
+        }

        lastUpdateTime = block.timestamp;
-        periodFinish = block.timestamp + rewardsDuration;
-        emit RewardAdded(_reward, _rewardUsdc);
    }

+    function _startRewardPeriod(uint256 _reward, uint256 _rewardUsdc) internal {
+        rewardRate = _reward / rewardsDuration;
+        rewardRateUSDC = _rewardUsdc / rewardsDuration;
+
+        lastUpdateTime = block.timestamp;
+        periodFinish = block.timestamp + rewardsDuration;
+
+        emit RewardAdded(_reward, _rewardUsdc);
+    }
```

Solution is based on how Sommelier team tackled the problem in the CellarStaking contract, see: 
https://github.com/PeggyJV/cellar-contracts/blob/afd970c36e9a520326afc888a11d40cdde75c6a7/src/CellarStaking.sol?#L219
