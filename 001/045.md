Original Steel Gecko

High

# USDC rewards round down to zero in `rewardPerTokenUSDC`

## Summary

Because of low USDC's decimals (6), using KWENTA's approach for USDC rewards will result in rounding down to zero due to precision loss in `rewardPerTokenUSDC()`, causing USDC rewards to be stuck in the contract.

## Vulnerability Detail

Whenever `updateReward` modifier is triggered (when staking, unstaking, or claiming rewards), `rewardPerTokenStoredUSDC` and `lastUpdateTime` are updated:

```solidity
    modifier updateReward(address _account) {
        _updateReward(_account);
        _;
    }

    function _updateReward(address _account) internal {
        rewardPerTokenStored = rewardPerToken();
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC();
        lastUpdateTime = lastTimeRewardApplicable();
```
```solidity
    function rewardPerTokenUSDC() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

        if (allTokensStaked == 0) {
            return rewardPerTokenStoredUSDC;
        }

        return
            rewardPerTokenStoredUSDC +
            (((lastTimeRewardApplicable() - lastUpdateTime) *
                rewardRateUSDC *
                1e18) / allTokensStaked);
    }
```
`((lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC * 1e18) / allTokensStaked` rounds down to zero if 

`(lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC * 1e18 < allTokensStaked`

Let's use the following values for the sake of example: 
`_rewardUsdc  = 10_000e6 USDC`
`rewardsDuration = 7 days`
`allTokensStaked = 500_000e18 KWENTA`
`rewardRateUSDC = _rewardUsdc / rewardsDuration = 1e10 / 604800 = 16534` *

replacing variables with their values:

`(lastTimeRewardApplicable() - lastUpdateTime) * 16534 * 1e18 < 500_000e18`
`(lastTimeRewardApplicable() - lastUpdateTime) < 500_000 / 16_534`
`(lastTimeRewardApplicable() - lastUpdateTime) < 30.24`

Hence, until `periodFinish`, **every time** someone triggers `_updateReward` earlier than in 31 seconds since `lastUpdateTime`, `rewardPerTokenUSDC` is not increased, but `lastUpdateTime` is still updated, so rewards for these 30 seconds are lost. This can also be achieved artificially, by an attacker calling `getReward` every 30 seconds.

**assuming `notifyRewardAmount` is called after `periodFinish`; if `notifyRewardAmount` is called before `periodFinish`, `rewardRateUSDC` would be slightly higher, but the issue would still exist - the attacker would just need to call `getReward` more often.*

## Impact

Stakers do not receive USDC rewards.

These USDC are stuck in the contract.

## Code Snippet
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/22ab9350c4ed90f1103b1cd80fc88c062da5fbcc/token/contracts/StakingRewardsV2.sol#L447-L456
## Proof of Concept
Add to StakingRewardsV2.t.sol:
```solidity
    function test_poc() public {
        uint256 initialUsdcBalance = usdc.balanceOf(address(this));

        // we stake 500_000 KWENTA (mocking all stakers)
        deal(address(kwenta), address(this), 500_000e18);
        kwenta.approve(address(stakingRewardsV2), 500_000e18);
        stakingRewardsV2.stake(500_000e18);

        // notifyRewardAmount with 10_000 USDC
        vm.prank(address(rewardsNotifier));
        stakingRewardsV2.notifyRewardAmount(0, 10_000e6);

        // griefer calls getReward for 1 day (prior staking is not necessary)
        for (uint256 i = 0; i < 2880; i++) {
            skip(30);
            stakingRewardsV2.getReward();
        }
        vm.stopPrank();

        // we claim reward
        stakingRewardsV2.getReward();

        // our reward did not increase
        assertEq(usdc.balanceOf(address(this)), initialUsdcBalance);
    }

```
## Tool used

Manual Review

## Recommendation

USDC's precision should be increased to avoid precision loss.

One of the ways to achieve that would be to increase USDC precision in `notifyRewardAmount` to match KWENTA's precision for internal accounting:
 
```diff
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
+       _rewardUsdc *= 1e12;
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;

        } else {
            uint256 remaining = periodFinish - block.timestamp;
            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }
```

When rewards [are paid](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L372-L382) in `getReward`, they should be divided by 1e12:

```diff
-       uint256 rewardUSDC = rewardsUSDC[_account];
+       uint256 rewardUSDC = rewardsUSDC[_account] / 1e12;
        if (rewardUSDC > 0) {
            // update state (first)
            rewardsUSDC[_account] = 0;
            
            // emit reward claimed event and index account
            emit RewardPaidUSDC(_account, rewardUSDC);

            // transfer token from this contract to the account
            // as newly issued rewards from inflation are now issued as non-escrowed
            usdc.transfer(_to, rewardUSDC);
```

Note that this would "break" some of the view functions, as they would return all USDC-related values inflated by a factor of 1e12.