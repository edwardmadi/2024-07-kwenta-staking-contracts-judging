Wonderful Turquoise Tadpole

Medium

# Precision loss when dealing with USDC token due to low amount of decimals

## Summary

All calculations for USDC have a considerable precision loss which will naturally result in users receiving less rewards and can also be abused by a malicious attacker to prevent users from ever receiving rewards in the form of USDC.

## Vulnerability Detail

The reward rate is calculated by dividing the amount of tokens by the amount of time in a period. USDC has 6 decimals and a week is 604 800 seconds. That means we are very likely to have precision loss if the amount of USDC is not big enough. As a result `rewardRateUSDC` will be usually between 1e1 and 1e4.

```solidity
    /// @inheritdoc IStakingRewardsV2
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
@>          rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```

If the totalSupply of staked tokens is big enough a malicious user can call `StakingRewards::getRewards` in order to update the `rewardPerTokenUSDC` through the `StakingRewardsV2::rewardPerTokenUSDC` function:

```solidity
    /// @inheritdoc IStakingRewardsV2
    function rewardPerTokenUSDC() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

        if (allTokensStaked == 0) {
            return rewardPerTokenStoredUSDC;
        }

        return rewardPerTokenStoredUSDC
 @>         + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC * 1e18) / allTokensStaked);
    }
```

Since the total supply is in kwenta tokens which have 18 decimals, the extra decimals of `allTokensStaked` will cancel out with the `*1e18`. After that if the time passed since the last update (`lastTimeRewardApplicable() - lastUpdateTime`) is a low number (e.g 10,100 seconds) and the amount of USDC rewards is not big enough due to how solidity math works the result might get rounded down to 0. That means that users will be unable to receive any USDC rewards for an undefined period of time. 
A malicious attacker is not even needed for such problems to arise. If 2 users interact with the contract very soon one after the other the same effect will be achieved unintentionally.

POC
```solidity
    function test_USDC_rewards_DOS() public {
        // fund so that staking can succeed
        uint256 stakedAmount = 100000e18;
        fundAndApproveAccountV2(address(this), stakedAmount);

        // check reward per token starts as 0
        assertEq(stakingRewardsV2.rewardPerToken(), 0);

        // set rewards
        uint256 reward = 1e18;
        vm.prank(address(rewardsNotifier));
        stakingRewardsV2.notifyRewardAmount(0, 100e6);

        // stake
        stakingRewardsV2.stake(stakedAmount);
        assertEq(stakingRewardsV2.totalSupply(), stakedAmount);

        //update the rewards every 100 seconds
        for(uint256 i=0;i<7 weeks/100;i++){
        vm.warp(block.timestamp + 100);
        vm.prank(address(1));
        stakingRewardsV2.getReward();
        }

        console.logUint(stakingRewardsV2.earnedUSDC(address(this))); // 0
        console.logUint(stakingRewardsV2.rewardPerTokenUSDC());  // 0
    }
```

## Impact

Rewards will not be distributed to users in case of a malicious attacker.
Contract will never be able to distribute all USDC rewards due to precision loss.

## Code Snippet

https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L652
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L454-L455

## Tool used

Manual Review
VS Code
Foundry

## Recommendation

Add a precision factor to all USDC related calculations