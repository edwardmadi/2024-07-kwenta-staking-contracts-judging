Rhythmic Cinnabar Rabbit

High

# Potential Zero Reward Rate Due to Insufficient Reward Amount

## Summary

The notifyRewardAmount() function calculates rewardRate and rewardRateUSDC by dividing _reward and _rewardUsdc by rewardsDuration. If these input amounts are less than rewardsDuration, the division will result in zero due to integer division.  
For example after a short time after launching the protocol

The notifyReward contract calls stakingRewardsV2.notifyRewardAmount(currentBalance, currentBalanceUsdc), where currentBalance and currentBalanceUsdc are the balances of KWENTA and USDC in the notifyReward contract. If either of these balances is less than the rewardsDuration (which is initially set to 1 week or 604,800 seconds), it will result in a zero reward rate for that token.
**This issue is compounded by the fact that:**

* **The rewardsDuration can be changed by the owner.**
* **The notifyRewardAmount function doesn't check for minimum reward amounts.**

https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L645
## Vulnerability Detail
PoC
```solidity
  function test_rewardPerTokenUSDC() public {
        // fund so that staking can succeed
        uint256 stakedAmount = 0.75 weeks;  //this is less than 1 week which is the current rewardDuration
        fundAndApproveAccountV2(address(this), stakedAmount);

        // check reward per token starts as 0
        assertEq(stakingRewardsV2.rewardPerTokenUSDC(), 0);

        // stake
        stakingRewardsV2.stake(stakedAmount);
        assertEq(stakingRewardsV2.totalSupply(), stakedAmount);

        // set rewards
        uint256 reward = stakedAmount;
        vm.prank(address(rewardsNotifier));
        stakingRewardsV2.notifyRewardAmount(reward, reward); // called the  notifyRewardAmount function

        emit log_uint(stakingRewardsV2.rewardRate());
        emit log_uint(stakingRewardsV2.rewardRateUSDC());
        assertEq(stakingRewardsV2.rewardRate(),0);  // RewardRate ==0
        assertEq(stakingRewardsV2.rewardRateUSDC(),0); // rewardRateUSDC = 0

        // ff to end of period
        vm.warp(block.timestamp + 0.75 weeks);

        // check reward per token updated
        assertEq(stakingRewardsV2.rewardPerTokenUSDC(), 0 ether); // rewardTokenUSDC is consequently =0
        assertEq(stakingRewardsV2.rewardPerToken(), 0 ether);  // rewardToken is consequently =0

        
    }

```
run `forge test --match-test test_rewardPerTokenUSDC -vvv`
Result

```solidity
⠢] Compiling...
[⠔] Compiling 7 files with Solc 0.8.19
[⠑] Solc 0.8.19 finished in 15.68s
Compiler run successful with warnings:
Warning (5667): Unused function parameter. Remove or comment out the variable name to silence this warning.
  --> contracts/StakingRewardsNotifier.sol:88:33:
   |
88 |     function notifyRewardAmount(uint256 mintedAmount) external onlySupplySchedule {
   |                                 ^^^^^^^^^^^^^^^^^^^^


Ran 1 test for test/foundry/unit/StakingRewardsV2/StakingRewardsV2.t.sol:StakingRewardsV2Test
[PASS] test_rewardPerTokenUSDC() (gas: 255068)
Logs:
  0
  0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.44ms (446.90µs CPU time)

Ran 1 test suite in 23.30ms (8.44ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)

```

## Impact
* If the balances are low, it could lead to periods where no rewards are distributed, despite the intention to provide rewards.
* This could result in unfair distribution of rewards, where some periods have rewards and others don't, based solely on the timing of the notification.
## Code Snippet
```solidity

  function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;  
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
```solidity
  function notifyRewardAmount(uint256 mintedAmount) external onlySupplySchedule {
        uint256 currentBalance = kwenta.balanceOf(address(this));
        kwenta.transfer(address(stakingRewardsV2), currentBalance);
        uint256 currentBalanceUsdc = usdc.balanceOf(address(this));
        usdc.transfer(address(stakingRewardsV2), currentBalanceUsdc);

        stakingRewardsV2.notifyRewardAmount(currentBalance, currentBalanceUsdc);
    }
}
```
## Tool used

Manual Review

## Recommendation
* Implement a minimum reward Check
```solidity
function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
    external
    onlyRewardsNotifier
    updateReward(address(0))
{
    require(_reward >= rewardsDuration, "Reward too low");
    require(_rewardUsdc >= rewardsDuration, "USDC reward too low");

    // ... rest of the function
}
```