Dapper Obsidian Squid

Medium

# If there are no tokens staked for a certain time period, the rewards during this time period is distributed to nobody, and is locked in StakingRewardsV2 contract.


## Summary

If there are no tokens staked for a certain time period, the rewards during this time period is distributed to nobody, and is locked in StakingRewardsV2 contract.

## Vulnerability Detail

Let's first see how rewards are distributed. The `StakingRewardsNotifier.sol` sends the reward tokens to `StakingRewardsV2.sol`, and calculates the rate of reward, which represents the number of reward tokens per second that should be distributed. The rates are stored in `rewardRate` and `rewardRateUSDC`. The calculation of the reward rate is done in the following function:

```solidity
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
@>          rewardRate = _reward / rewardsDuration;
@>          rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
@>          rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
@>          rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```

Now, for the `StakingRewardsV2.sol` contract, each time before the totalSupply (total amount of staked tokens) is updated, the `_updateReward()` is called to update the latest reward token related variables. There are three important variables:

1. `rewardPerTokenStored`, the number of reward tokens (in kwenta) a user should receive per staked token.
2. `rewardPerTokenStoredUSDC`, the number of reward tokens (in usdc) a user should receive per staked token.
3. `lastUpdateTime`, the last time the above two variables were updated.

The issue is, in the functions `rewardPerToken()` and `rewardPerTokenUSDC()`, we can see if the total amount of staked tokens is 0, the above two variables are NOT updated. This makes sense, since there is no staked token to distribute the rewards to. However, the issue is, even if `allTokensStaked` is zero, `lastUpdateTime` is still updated to the latest reward timestamp.

This means that for a specific time period, if the total amount of staked tokens is zero, the amount of rewards that should've been distributed in this time period will NOT be distrbuted, and be fully locked. The correct way to handle this is to distribute these reward tokens to the future stakers, instead of locking them.

There is a `recoverERC20()` function to recover the reward tokens, however, since this specific function cannot recover kwenta token, the kwenta reward tokens would still be locked.

An numerical PoC of the issue:

1. The `rewardRate` is 100e18/second.
2. At time T0, the last user staking in this contract unstakes their token, and the total amount of staked token turns to 0.
3. At time T1, another user comes and stakes.
4. `_updateReward()` is called, but `lastUpdateTime` is updated to T1, while `rewardPerTokenStored` and `rewardPerTokenStoredUSDC` is not updated.

This means the reward tokens 100e18 * (T1 - T0) amount of kwenta tokens are locked in the contract, and with no way to claim it.

A solution to fixing this issue is to simply do nothing in `_updateReward()` if `allTokensStaked == 0`.

```solidity
    function _updateReward(address _account) internal {
        rewardPerTokenStored = rewardPerToken();
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC();
@>      lastUpdateTime = lastTimeRewardApplicable();

        if (_account != address(0)) {
            // update amount of rewards a user can claim
            rewards[_account] = earned(_account);

            // update reward per token staked AT this given time
            // (i.e. when this user is interacting with StakingRewards)
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;

            rewardsUSDC[_account] = earnedUSDC(_account);

            userRewardPerTokenPaidUSDC[_account] = rewardPerTokenStoredUSDC;
        }
    }

    /// @inheritdoc IStakingRewardsV2
    function rewardPerToken() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

@>      if (allTokensStaked == 0) {
            return rewardPerTokenStored;
        }

        return rewardPerTokenStored
            + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / allTokensStaked);
    }

    /// @inheritdoc IStakingRewardsV2
    function rewardPerTokenUSDC() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

@>      if (allTokensStaked == 0) {
            return rewardPerTokenStoredUSDC;
        }

        return rewardPerTokenStoredUSDC
            + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC * 1e18) / allTokensStaked);
    }

    function lastTimeRewardApplicable() public view returns (uint256) {
        return block.timestamp < periodFinish ? block.timestamp : periodFinish;
    }    
```

## Impact

If there are no tokens staked for a certain time period, the rewards during this time period is distributed to nobody, and is locked in StakingRewardsV2 contract.

## Code Snippet

- https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L410-L427

## Tool used

Manual Review

## Recommendation

Do nothing in `_updateReward()` if `allTokensStaked == 0`.