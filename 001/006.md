Wide Fuchsia Elephant

Medium

# Significant amounts of USDC tokens are not distributed as rewards.

## Summary
`StakingRewardsV2.sol#notifyRewardAmount` function has rounding error in calculation.
Since USDC token has only 6 decimals, significant amounts of USDC tokens are not distributed as rewards.

## Vulnerability Detail
`StakingRewardsV2.sol#notifyRewardAmount` function is the following.
```solidity
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
652:        rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
660:        rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
As can be seen, the calculation of `rewradRateUSDC` has rounding error in `L652` and `L660`.
USDC token has only 6 decimals and `rewardsDuration` has initial value `1 weeks = 7 * 86400 = 604_800`.
So the rounding error can be up to `0.6$`.
Moreover, `rewardsDuration` is not immutable variable and can be updated by admin to the more larger value.

## Impact
Up to `0.6$` USDC tokens can't be distributed per week and will be locked in the `StakingRewardsV2`.
The loss will be greater when admin updates `rewardsDuration` to the larger value.
The same arises for kwenta tokens but the amounts are small because it has 18 decimals.

## Code Snippet
- [token/contracts/StakingRewardsV2.sol#L652](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/tree/main/token/contracts/StakingRewardsV2.sol#L652)
- [token/contracts/StakingRewardsV2.sol#L660](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/tree/main/token/contracts/StakingRewardsV2.sol#L660)

## Tool used

Manual Review

## Recommendation
To decrease the rounding error, introduce a appropriate multiplier for `rewradRateUSDC`.
For instance, modify `StakingRewardsV2.sol#notifyRewardAmount` function as follows.
```solidity
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
--          rewardRateUSDC = _rewardUsdc / rewardsDuration;
++          rewardRateUSDC = _rewardUsdc * 1e18 / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

--          uint256 leftoverUsdc = remaining * rewardRateUSDC;
--          rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
++          uint256 leftoverUsdc = remaining * rewardRateUSDC / 1e18;
++          rewardRateUSDC = (_rewardUsdc + leftoverUsdc) * 1e18 / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
And then modify `rewardPerTokenUSDC` function as follows.
```solidity
    function rewardPerTokenUSDC() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

        if (allTokensStaked == 0) {
            return rewardPerTokenStoredUSDC;
        }

        return rewardPerTokenStoredUSDC
--          + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC * 1e18) / allTokensStaked);
++          + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRateUSDC) / allTokensStaked);
    }
```
