Feisty Indigo Giraffe

High

# Rewards sent to the staking rewards contract can get stuck and won't be recoverable.

## Summary
If rewards have been notified when there are no stakers i.e when totalSupply is zero then until someone stakes the rewards till then will be stuck in the contract and they can't be redistributed in the next cycle. Plus they are not even recoverable.
## Vulnerability Detail
Following is notify reward function 
```solidity
function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
It first calls updateReward(address(0)) which does the following
```solidity
 modifier updateReward(address _account) {
        _updateReward(_account);
        _;
    }

    function _updateReward(address _account) internal {
        rewardPerTokenStored = rewardPerToken();
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC();
        lastUpdateTime = lastTimeRewardApplicable();

        if (_account != address(0)) {
            // update amount of rewards a user can claim
            rewards[_account] = earned(_account);

            // update reward per token staked AT this given time
            // (i.e. when this user is interacting with StakingRewards)
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;

            rewardsUSDC[_account] = earnedUSDC(_account);

            userRewardPerTokenPaidUSDC[_account] = rewardPerTokenStoredUSDC;
        }
    }
```
As we main core of the issue is that totalSupply is zero so both of the following values will be set as zero
 ```solidity
        rewardPerTokenStored = rewardPerToken(); = 0
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC(); = 0
```
The above value are zero can be easily seen from the following function
```solidity
function rewardPerToken() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

        if (allTokensStaked == 0) {
            return rewardPerTokenStored;
        }

        return rewardPerTokenStored
            + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / allTokensStaked);
    }
```
if condition will be executed and as initially rewardPertokenStored = 0 therefore it will return zero. Similarly rewardPerTokenStoredUsdc will be also zero.

Now suppose one third reward duration has passed and now someone stakes. Now the user would be only eligible for the 2/3rd amount of rewards left for that particular cycle which is correct. 

Now issue is that the rest 1/3 rd rewards would be left stuck in the contract because owner cannot recover kwenta tokens  as can be seen from the following function it reverts if token address is kwenta.
```solidity
 function recoverERC20(address _tokenAddress, uint256 _tokenAmount) external onlyOwner {
        if (_tokenAddress == address(kwenta)) revert CannotRecoverStakingToken();
        emit Recovered(_tokenAddress, _tokenAmount);
        IERC20(_tokenAddress).transfer(owner(), _tokenAmount);
    }
```
One argument against this finding can be that the rewards which weren't distributed in the initial cycle can be rolled over to the next cycle but that is also not possible because notifyRewardAmount function is only allowed to be called by rewards notifier contract.
Rewards notifier contract perform that action as follows
```solidity
function notifyRewardAmount(uint256 mintedAmount) external onlySupplySchedule {
        uint256 currentBalance = kwenta.balanceOf(address(this));
        kwenta.transfer(address(stakingRewardsV2), currentBalance);
        uint256 currentBalanceUsdc = usdc.balanceOf(address(this));
        usdc.transfer(address(stakingRewardsV2), currentBalanceUsdc);

        stakingRewardsV2.notifyRewardAmount(currentBalance, currentBalanceUsdc);
    }
```
As can be seen from above that there is no way to notify the staking rewards contract to redistribute the stuck 1/3rd rewards. The  rewards are only notified by the amount of tokens present in the notifier contract so there is no way to redistribute the tokens which weren't distributed in the previous cycle.
## Impact
Tokens can get stuck in the staking rewards contract.
## Code Snippet
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L710
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L645
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsNotifier.sol#L88
## Tool used

Manual Review

## Recommendation
In recover function allow kwenta tokens to also be recovered. Maybe introduce a mechanism to redistribute the left over tokens in the contract.