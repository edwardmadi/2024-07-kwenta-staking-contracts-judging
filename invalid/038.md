Dapper Obsidian Squid

Medium

# Cooldown period does not work as documented: user does not have to wait for another cooldown period after claming rewards.


## Summary

Cooldown period does not work as documented: user does not have to wait for another cooldown period after claming rewards.

## Vulnerability Detail

Let's see the definition of the cooldown period https://docs.kwenta.io/kwenta-token/staking-kwenta#cooldown-period.

> The cooldown period is NEW, and the time required before unstaking your tokens is possible. The cooldown period to unstake your tokens is two weeks & the clock starts ticking from the last time a staker interacted with the v2 staking contracts. This includes adding to your staked balance with liquid $KWENTA or escrow $KWENTA. 

> Example:

> If the cooldown period was activated 7 days and 2 hours ago, you claim rewards and will now need to start the 7-day cooldown period again. 

However, the current code implementation only updates the cooldown period upon user staking actions. When user claims rewards by `getReward()`, the cooldown period is not updated.

```solidity
    function getReward() external {
        _getReward(msg.sender);
    }

    function _getReward(address _account, address _to)
        internal
        whenNotPaused
        updateReward(_account)
    {
        uint256 reward = rewards[_account];
        if (reward > 0) {
            // update state (first)
            rewards[_account] = 0;

            // emit reward claimed event and index account
            emit RewardPaid(_account, reward);

            // transfer token from this contract to the rewardEscrow
            // and create a vesting entry at the _to address
            kwenta.transfer(address(rewardEscrow), reward);
            rewardEscrow.appendVestingEntry(_to, reward);
        }

        uint256 rewardUSDC = rewardsUSDC[_account];
        if (rewardUSDC > 0) {
            // update state (first)
            rewardsUSDC[_account] = 0;

            // emit reward claimed event and index account
            emit RewardPaidUSDC(_account, rewardUSDC);

            // transfer token from this contract to the account
            // as newly issued rewards from inflation are now issued as non-escrowed
            usdc.transfer(_to, rewardUSDC);
        }
    }
```

## Poc

Add the following code in `StakingV2CooldownPeriod.t.sol`. There are five steps:

1. Stake
2. Try unstake 1 sec before period ends, but fails.
3. Try unstake 1 sec after period ends, succeed.
4. Claim rewards.
5. Try to unstake after claiming reward, it should fail, but does NOT.

```solidity
    function test_cooldown_bug() public {
        // 1. Stake
        fundAccountAndStakeV2(address(this), TEST_VALUE);

        uint256 cooldownPeriod = stakingRewardsV2.cooldownPeriod();
        uint256 canUnstakeAt = block.timestamp + cooldownPeriod;
        uint256 stakedAt = block.timestamp;

        // 2. Try unstake 1 sec before period ends, but fails.
        vm.warp(stakedAt + cooldownPeriod - 1);
        vm.expectRevert(
            abi.encodeWithSelector(IStakingRewardsV2.MustWaitForUnlock.selector, canUnstakeAt)
        );
        stakingRewardsV2.unstake(TEST_VALUE / 2);

        // 3. Try unstake 1 sec after period ends, succeed.
        vm.warp(stakedAt + cooldownPeriod + 1);
        stakingRewardsV2.unstake(TEST_VALUE / 2);

        // 4. Claim.
        stakingRewardsV2.getReward();

        // 5. The unstake should revert, but does NOT.
        stakingRewardsV2.unstake(TEST_VALUE / 2);
    }
```

## Impact

Cooldown period does not work as documented: user does not have to wait for another cooldown period after claming rewards.

## Code Snippet

- https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L353-L384

## Tool used

Foundry

## Recommendation

Also update the cooldown period in the `_getReward()` function.