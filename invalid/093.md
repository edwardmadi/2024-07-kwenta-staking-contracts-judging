Modern Grape Bull

Medium

# If the previous reward phase has not yet ended and notifyRewardAmount injects a new reward, it may increase the time required for users to receive the same reward

## Summary
If the previous reward phase has not yet ended and notifyRewardAmount injects a new reward, the rewardRate will be recalculated, and the periodFinish will be reset. This may result in an extended time required for users to receive the same reward.
## Vulnerability Detail
Assuming that only Bob is staking in the staking pool and the Cooldown time has passed, a reward of 1000 USDC has been injected with a rewardsDuration of 10 days. Bob only wants to unstake in the tenth day and expects to receive a reward of 1000 USDC.Now rewardRate is 100 USDC/days 
On the fifth day, a new reward of 300 USDC was injected,  rewardRate become (500 + 300) / 10 = 80 USDC/day.  
So on the tenth day, Bob can only receive 900 USDC(100 * 5 + 80 * 5)  

POCï¼š
```solidity
    function test_2() public {
        uint256 rewardsDurantion = stakingRewardsV2.rewardsDuration();

        console2.log(rewardsDurantion);
        address Alice = makeAddr("Alice");

        fundAndApproveAccountV2(Alice, 3 ether);

        vm.startPrank(Alice);
        stakingRewardsV2.stake(3 ether);
        vm.stopPrank();

        vm.warp(block.timestamp + 4 weeks);

        deal(address(usdc), address(rewardsNotifier), 1000e6);
        vm.startPrank(address(supplySchedule));
        rewardsNotifier.notifyRewardAmount(1000);
        vm.stopPrank();

        vm.warp(block.timestamp + rewardsDurantion / 2);

        deal(address(usdc), address(rewardsNotifier), 300e6);
        vm.startPrank(address(supplySchedule));
        rewardsNotifier.notifyRewardAmount(300);
        vm.stopPrank();

        vm.warp(block.timestamp + rewardsDurantion / 2);
        vm.startPrank(Alice);
        stakingRewardsV2.exit();
        vm.stopPrank();

        console2.log("usdc balance: %s", usdc.balanceOf(address(Alice)));
    }
```
log:
```bash
[PASS] test_2() (gas: 678368)
Logs:
  604800
  usdc balance: 899640000
```
From the logs, it can be seen that Alice received only 900 USDC in rewards when she unstaked 10 days after the first reward injection.
## Impact
Users will receive fewer staking rewards than expected during the same time
## Code Snippet
github:[https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L645](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L645)
```solidity
    /// @inheritdoc IStakingRewardsV2
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
## Tool used

Manual Review

## Recommendation
It is recommended that no new rewards be added before the end of the previous reward period
```solidity
    /// @inheritdoc IStakingRewardsV2
    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
+     require(block.timestamp >= periodFinish, "last rewards duration has not ended")
-      if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
-        } else {
-           uint256 remaining = periodFinish - block.timestamp;

-            uint256 leftover = remaining * rewardRate;
-            rewardRate = (_reward + leftover) / rewardsDuration;
-
-            uint256 leftoverUsdc = remaining * rewardRateUSDC;
-            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
-        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```