Faint Mercurial Buffalo

High

# Inefficient Reward Distribution Calculation Causes Tokens to Become Stuck inside the Contract

## Summary

The `StakingRewardsV2` contract has a vulnerability in its reward distribution mechanism. When a new reward cycle is initiated using `notifyRewardAmount()`, a fixed `periodFinish` is set based on the current timestamp. If there's a delay between the start of the reward period and the first stake, a portion of the rewards becomes inaccessible to stakers, effectively getting stuck in the contract.

## Vulnerability Details

The vulnerability in the `StakingRewardsV2` contract arises from how it handles the initiation of new reward cycles via the `notifyRewardAmount()` function. This function sets a fixed `periodFinish` timestamp based on the current block timestamp. If there is a delay between executing `notifyRewardAmount()` and the first staking action, the rewards meant to be distributed during this initial period become inaccessible. This happens because the contract assumes rewards should be evenly distributed over the entire period, regardless of when staking begins. Consequently, rewards that were supposed to be distributed during the delay period get locked in the contract, leaving stakers with less than the intended total reward amount. This inefficiency results in reduced rewards for stakers and undermines the fairness and reliability of the reward distribution mechanism.

## Code Snippet

https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/main/token/contracts/StakingRewardsV2.sol#L645-L666

## Impact

This vulnerability leads to:

1. Inefficient use of reward tokens: A portion of the allocated rewards remains undistributed.
2. Discrepancy between intended and actual reward distribution: Stakers receive fewer rewards than intended.
3. Accumulation of stuck tokens: Over multiple reward cycles, the amount of inaccessible rewards can accumulate.
4. Loss of Funds: Stuck tokens cannot be recovered or redistributed, leading to a permanent loss of value.

The severity of this issue is considered HIGH. Due to inefficient reward distribution, stakers may receive fewer rewards than expected, leading to a loss of value for participants. Additionally, the stuck tokens cannot be recovered.

## Proof of Concept (PoC)

Let's consider a scenario with the following parameters:

- Reward duration: 1 week (604,800 seconds)
- Total reward: 604,800 KWENTA tokens (1 KWENTA per second)
- Alice stakes 100e18 KWENTA tokens
- Delay between reward notification and first stake: 50 minutes (3,000 seconds)
- Alice calls `getReward()` after the reward duration ends (604,800 seconds after reward notification)

### Step 1: Notify Reward Amount

At block N, timestamp X:

```solidity
function notifyRewardAmount(uint256 _reward) external onlyRewardsNotifier updateReward(address(0)) {
    rewardRate = _reward / rewardsDuration;
    lastUpdateTime = block.timestamp;
    periodFinish = block.timestamp + rewardsDuration;
    emit RewardAdded(_reward);
}
```

Calculations:

- `rewardRate = 604800 / 604800 = 1 KWENTA per second`
- `lastUpdateTime = X`
- `periodFinish = X + 604800`

### Step 2: Alice Stakes (50 minutes later)

At block M, timestamp X + 3000:

```solidity
function stake(uint256 _amount) external updateReward(msg.sender) {
    // ... (existing code)
}

modifier updateReward(address _account) {
    rewardPerTokenStored = rewardPerToken();
    lastUpdateTime = lastTimeRewardApplicable();
    if (_account != address(0)) {
        rewards[_account] = earned(_account);
        userRewardPerTokenPaid[_account] = rewardPerTokenStored;
    }
    _;
}
```

Calculations in `updateReward`:

1. `rewardPerToken()`:
   - `totalSupply() == 0`, so `return rewardPerTokenStored; // 0`
2. `lastUpdateTime = lastTimeRewardApplicable() = min(X + 3000, periodFinish) = X + 3000`
3. `rewards[alice] = earned(alice) = 0` (first stake)
4. `userRewardPerTokenPaid[alice] = rewardPerTokenStored = 0`

After staking:

- `totalSupply = 100e18`
- `balanceOf[alice] = 100e18`

### Step 3: Alice calls getReward (after reward duration ends)

At block P, timestamp X + 604800:

```solidity
function getReward() external updateReward(msg.sender) {
    uint256 reward = rewards[msg.sender];
    if (reward > 0) {
        rewards[msg.sender] = 0;
        rewardToken.safeTransfer(msg.sender, reward);
        emit RewardPaid(msg.sender, reward);
    }
}
```

Calculations in `updateReward`:

1. `rewardPerToken()`:

   ```javascript
   rewardPerTokenStored +
   (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / totalSupply())
   = 0 + (((X + 604800) - (X + 3000)) * 1 * 1e18) / 100e18
   = (601800 * 1e18) / 100e18
   = 6018e18
   ```

2. `lastUpdateTime = lastTimeRewardApplicable() = X + 604800`
3. `rewards[alice] = earned(alice)`:

   ```javascript
   (balanceOf(alice) * (rewardPerToken() - userRewardPerTokenPaid[alice])) / 1e18 + rewards[alice]
   = (100e18 * (6018e18 - 0)) / 1e18 + 0
   = 601800e18
   ```

4. `userRewardPerTokenPaid[alice] = rewardPerTokenStored = 6018e18`

After `getReward`:

- Alice receives `601800e18` KWENTA tokens (601,800 KWENTA)

### Calculation of Stuck Tokens

- Intended reward distribution for the entire duration: 604,800 KWENTA
- Actual reward distribution: 601,800 KWENTA
- Stuck tokens: 604,800 - 601,800 = 3,000 KWENTA

In this scenario, 3,000 KWENTA tokens (approximately 0.496% of the total reward) remain stuck in the contract and are not distributed to any staker. This corresponds to the 50 minutes (3,000 seconds) delay between the reward notification and Alice's stake.

### Second Reward Cycle

- Total reward: 604,800 KWENTA tokens
- Bob stakes 100e18 KWENTA tokens immediately after `notifyRewardAmount`
- Bob calls `getReward()` after the reward duration ends

### Step 4: Notify Reward Amount (Second Cycle)

At block Q, timestamp Y (where Y = X + 604800):

```solidity
function notifyRewardAmount(uint256 _reward) external onlyRewardsNotifier updateReward(address(0)) {
    uint256 remaining = periodFinish - block.timestamp;
    if (remaining > 0) {
        rewardRate = ((_reward + (remaining * rewardRate)) / rewardsDuration);
    } else {
        rewardRate = _reward / rewardsDuration;
    }
    lastUpdateTime = block.timestamp;
    periodFinish = block.timestamp + rewardsDuration;
    emit RewardAdded(_reward);
}
```

Calculations:

- `remaining = 0` (as we're exactly at the end of the first cycle)
- `rewardRate = 604800 / 604800 = 1 KWENTA per second`
- `lastUpdateTime = Y`
- `periodFinish = Y + 604800`

### Step 5: Bob Stakes (Immediately)

At block Q (same as notify), timestamp Y:

Calculations in `updateReward`:

1. `rewardPerToken() = 6018e18` (carried over from last cycle)
2. `lastUpdateTime = Y`
3. `rewards[bob] = 0` (first stake for Bob)
4. `userRewardPerTokenPaid[bob] = 6018e18`

After staking:

- `totalSupply = 200e18`
- `balanceOf[bob] = 100e18`

### Step 6: Bob calls getReward (after second reward duration ends)

At block R, timestamp Y + 604800:

Calculations in `updateReward`:

1. `rewardPerToken()`:

   ```javascript
   6018e18 + (((Y + 604800) - Y) * 1 * 1e18) / 200e18
   = 6018e18 + 3024e18
   = 9042e18
   ```

2. `lastUpdateTime = Y + 604800`
3. `rewards[bob]`:

   ```javascript
   (100e18 * (9042e18 - 6018e18)) / 1e18 + 0
   = 302400e18
   ```

4. `userRewardPerTokenPaid[bob] = 9042e18`

After `getReward`:

- Bob receives 302,400 KWENTA tokens

## Analysis of Stuck Tokens

In the second cycle, the total rewards distributed are:

- To Alice: (100e18 \* (9042e18 - 6018e18)) / 1e18 = 302,400 KWENTA
- To Bob: 302,400 KWENTA
- Total: 604,800 KWENTA

This means that all tokens from the second reward cycle were distributed. However, the 3,000 KWENTA tokens that were stuck from the first cycle remain inaccessible. They are effectively lost within the contract and cannot be distributed to stakers in subsequent cycles too.

### Proof Of Code

Place the following code into the `StakingV2RewardCalculations.t.sol` file:

```javascript
function test_Bluedragon_RewardsGetsStuck() public {
        uint256 lengthOfPeriod = 1 weeks;
        uint256 initialStake = 100 ether;

        // send in 604800 (1 week) of rewards - (using 1 week for round numbers)
        addNewRewardsToStakingRewardsV2(1 weeks, 0);

        // Alice stakes after 50 minutes from the reward period start
        vm.warp(50 minutes);

        // stake after 50 min
        fundAccountAndStakeV2(user1, initialStake);

        // fast forward 1 week - one complete period
        vm.warp(block.timestamp + lengthOfPeriod);

        // Alice calls get reward function
        getStakingRewardsV2(user1);

        uint256 firstRewards = rewardEscrowV2.escrowedBalanceOf(user1);
        console.log("Rewards distributed to alice in first cycle: ", firstRewards);
        uint256 stuckTokens = kwenta.balanceOf(address(stakingRewardsV2));
        console.log("Tokens stuck in contract: ", stuckTokens - initialStake);

        // Second reward cycle starts
        addNewRewardsToStakingRewardsV2(1 weeks, 0);

        // Bob stakes as soon as the new reward period starts
        fundAccountAndStakeV2(user2, initialStake);

        // fast forward 1 week - one complete period
        vm.warp(block.timestamp + lengthOfPeriod);

        // Bob calls get reward function
        getStakingRewardsV2(user2);
        // Alice calls get reward function
        getStakingRewardsV2(user1);

        uint256 secondRewards_alice = rewardEscrowV2.escrowedBalanceOf(user1);
        console.log(
            "Rewards distributed to alice in second cycle: ", secondRewards_alice - firstRewards
        );
        uint256 secondRewards_bob = rewardEscrowV2.escrowedBalanceOf(user2);
        console.log("Rewards distributed to bob in second cycle: ", secondRewards_bob);
        uint256 stuckTokens_2 = kwenta.balanceOf(address(stakingRewardsV2));
        console.log("Tokens stuck in contract: ", stuckTokens_2 - (2 * initialStake));
    }
```

Here's the output of the test:

```javascript
Ran 1 test for test/foundry/unit/StakingRewardsV2/StakingV2RewardCalculations.t.sol:StakingV2RewardCalculationTests
[PASS] test_Bluedragon_RewardsGetsStuck() (gas: 1106992)
Logs:
  Rewards distributed to alice in first cycle:  601800
  Tokens stuck in contract:  3000
  Rewards distributed to alice in second cycle:  302400
  Rewards distributed to bob in second cycle:  302400
  Tokens stuck in contract:  3000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.33ms (1.53ms CPU time)
```

## Tools Used

- Manual Analysis
- Foundry

## Recommendations

To address this issue, consider modifying the `stake` function to update the `periodFinish` when the first stake occurs after a new reward cycle has been initiated:

```diff
function stake(uint256 _amount) external updateReward(msg.sender) {
+    if (totalSupply() == 0 && block.timestamp < periodFinish) {
+        uint256 timeElapsed = block.timestamp - lastUpdateTime;
+        periodFinish = block.timestamp + (rewardsDuration - timeElapsed);
+        lastUpdateTime = block.timestamp;
    }
    // ... (rest of the existing stake logic)
}
```

This change ensures that the full reward amount is distributed over the intended duration, starting from the first stake, thus preventing rewards from getting stuck in the contract.
