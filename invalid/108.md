Blunt Fuzzy Quail

High

# Cooldown reset on additional staking causes unintended extended token locking

### Summary

The staking contract's cooldown mechanism resets the cooldown period for the entire staked balance when a new stake is made. This includes previously staked tokens, which inadvertently locks tokens that would otherwise be eligible for withdrawal.

The issue arises because the contract does not differentiate between tokens staked at different times, instead applying the most recent cooldown period to the entire staked amount.

### Root Cause

In `StakingRewardsV2.sol:236`, the [`stake()`](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L236-L249) function set the last stake time for a user without considering already staked tokens:

```solidity
function stake(uint256 _amount) external whenNotPaused updateReward(msg.sender) {
    if (_amount == 0) revert AmountZero();

@>  userLastStakeTime[msg.sender] = block.timestamp;
    _addTotalSupplyCheckpoint(totalSupply() + _amount);
    // .. .
}
```
This reset affects the unstaking process, as the [`unstake()`](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L252-L271) function uses a cooldown check based on this timestamp:

```solidity
function unstake(uint256 _amount)
    public
    whenNotPaused
    updateReward(msg.sender)
@>  afterCooldown(msg.sender)
{
   // ...
}

function _afterCooldown(address _account) internal view {
@>  uint256 canUnstakeAt = userLastStakeTime[_account] + cooldownPeriod;
    if (canUnstakeAt > block.timestamp) revert MustWaitForUnlock(canUnstakeAt);
}
```

This leads to unintended locking of previously staked tokens. For example, if a user stakes 100 tokens, waits for the cooldown period, then stakes an additional 1 token, they will be unable to unstake any of their 101 tokens until the cooldown period passes again. This effectively resets the cooldown for all staked tokens, not just the newly staked ones.

### Internal pre-conditions

Consider the following scenario:

- Initial cooldown period is set to two weeks.
- User stakes 1000 tokens at T=0
- At T=2 weeks (cooldown period), user unstakes 500 tokens
- At T=2 weeks + 1 day, user stakes 10 more tokens
- User attempts to unstake 500 tokens (which should be available) but fails


_Calculations:_

- T=0: Staked balance = 1000
- T=2 weeks: Staked balance = 500 (after unstaking)
- T=2 weeks + 1 day: Staked balance = 510 (after new stake)
- Expected unstakeable amount at this point = 500
- Actual unstakeable amount = 0 (all tokens locked for another 2 weeks)

This scenario demonstrates how a small additional stake can unexpectedly lock a large amount of previously unstakeable tokens.

### Impact

This vulnerability significantly affects user experience and undermines trust in the staking system.

Users may unexpectedly find their tokens locked for extended periods, preventing them from unstaking or participating in other DeFi opportunities. This can lead to financial losses or opportunity costs, as users might miss out on potential gains elsewhere. The unexpected extension of the locking period could discourage users from participating in the staking system altogether.

### PoC

Copy the following test in the test suite and run it with `forge test --mt testStakeBeforeUnstakeCooldown`
```solidity
uint256 constant INITIAL_BALANCE = 1000 ether;
uint256 constant STAKE_AMOUNT = 100 ether;
uint256 constant ESCROW_AMOUNT = 50 ether;

function setUp() public override {
    super.setUp();
    fundAndApproveAccountV2(address(this), INITIAL_BALANCE);
}

function testStakeBeforeUnstakeCooldown() public {
    // Initial stake
    stakingRewardsV2.stake(STAKE_AMOUNT);
    assertEq(stakingRewardsV2.nonEscrowedBalanceOf(address(this)), STAKE_AMOUNT, "Initial stake should be STAKE_AMOUNT");

    // Try to unstake immediately (should fail)
    vm.expectRevert(abi.encodeWithSelector(IStakingRewardsV2.MustWaitForUnlock.selector, block.timestamp + 2 weeks));
    stakingRewardsV2.unstake(STAKE_AMOUNT);

    // Advance time to meet cooldown
    vm.warp(block.timestamp + 2 weeks);
    stakingRewardsV2.unstake(STAKE_AMOUNT / 2);

    // Stake more
    stakingRewardsV2.stake(1);
    assertEq(stakingRewardsV2.nonEscrowedBalanceOf(address(this)), STAKE_AMOUNT/2 + 1, "Stake should be STAKE_AMOUNT/2 + 1");

    // Unstaking everything should not succeed
    vm.expectRevert(abi.encodeWithSelector(IStakingRewardsV2.MustWaitForUnlock.selector, block.timestamp + 2 weeks));
    stakingRewardsV2.unstake(STAKE_AMOUNT / 2 + 1);

    // Attempting to unstake the initial amount should have succeeded!
    vm.expectRevert(abi.encodeWithSelector(IStakingRewardsV2.MustWaitForUnlock.selector, block.timestamp + 2 weeks));
    stakingRewardsV2.unstake(STAKE_AMOUNT / 2);
}
```

### Mitigation

Leverage the new checkpoint feature to implement a more granular cooldown tracking system:

- For each stake action, create a new checkpoint that includes the amount staked, the remaining amount not yet unstaked, timestamp of stake and the cooldown end time (timestamp + cooldown period)
- Update the unstake function to iterate through the checkpoints. Calculate the total unstakeable amount based on the checkpoints where the cooldown period has ended. Permit unstaking up to this total amount.
- When a new stake is made, generate a new checkpoint for this specific stake amount. Do not alter the existing checkpoints, ensuring that previously staked amounts retain their original cooldown timings.
- Implement a mechanism to remove or consolidate checkpoints once all tokens in those checkpoints are unstakeable, preventing unbounded growth of the checkpoint array and optimizing gas costs.

Example pseudocode:
```solidity
struct StakeCheckpoint {
    uint128 amount; // Amount staked in this checkpoint
    uint128 remainingAmount; // Amount still staked (not yet unstaked)
    uint64 stakeTime; // Timestamp when the stake was made
    uint64 cooldownEndTime; // Timestamp when the cooldown period ends for this stake
}

mapping(address => StakeCheckpoint[]) public userStakeCheckpoints;

function stake(uint256 amount) external {
    // ... 

    // Create a new checkpoint for this stake
    userStakeCheckpoints[msg.sender].push(StakeCheckpoint({
        amount: amount,
        remainingAmount: amount,
        stakeTime: block.timestamp,
        cooldownEndTime: block.timestamp + cooldownPeriod
    }));
}

function unstake(uint256 amount) external {
    uint256 unstakeableAmount = 0;
    uint256 toUnstake = _amount;
    
    for (uint i = 0; i < userStakeCheckpoints[msg.sender].length; i++) {
        StakeCheckpoint storage checkpoint = userStakeCheckpoints[msg.sender][i];
        
        if (block.timestamp >= checkpoint.cooldownEndTime && checkpoint.remainingAmount > 0) {
            uint256 availableFromCheckpoint = checkpoint.remainingAmount;
            unstakeableAmount += availableFromCheckpoint;
            
            if (toUnstake > 0) {
                uint256 unstakeFromCheckpoint = availableFromCheckpoint < toUnstake ? availableFromCheckpoint : toUnstake;
                checkpoint.remainingAmount -= uint128(unstakeFromCheckpoint);
                toUnstake -= unstakeFromCheckpoint;
            }
        }
        
        if (toUnstake == 0) break;
    }
    
    require(amount <= unstakeableAmount, "Cannot unstake more than allowed");
    require(toUnstake == 0, "Not enough unstakeable tokens");
    
    // ...
}
```