Jolly Gauze Donkey

Medium

# Function `notifyRewardAmount()` does not calculate the reward correctly when `totalSupply() == 0`

## Summary
The `StakingRewardsV2` contract is supposed to distribute rewards to stakers, but if, in some period, `totalSupply()` is equal to 0, then for that time period, rewards will not be added to `rewardPerTokenStored`. Consequently, those period rewards will not be distributed to any address and will remain stuck in the contract.

## Vulnerability Detail
This is `notifyRewardAmount()` code in the `StakingRewardsV2` contract:
```solidity

    function notifyRewardAmount(uint256 _reward, uint256 _rewardUsdc)
        external
        onlyRewardsNotifier
        updateReward(address(0))
    {
        if (block.timestamp >= periodFinish) {
            rewardRate = _reward / rewardsDuration;
            rewardRateUSDC = _rewardUsdc / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;

            uint256 leftover = remaining * rewardRate;
            rewardRate = (_reward + leftover) / rewardsDuration;

            uint256 leftoverUsdc = remaining * rewardRateUSDC;
            rewardRateUSDC = (_rewardUsdc + leftoverUsdc) / rewardsDuration;
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(_reward, _rewardUsdc);
    }
```
As you can see, in the line `rewardRate = _reward / rewardsDuration;`, the value of `rewardRate` has been set to the division of available reward by duration. Therefore, if we distribute the `rewardRate` amount every second among stakers, all rewards will be utilized by the contract. The contract uses the `updateReward()` modifier to update `rewardPerTokenStored` (this variable keeps track of distributed tokens), and this modifier calls the internal function `_updateReward()` which uses `rewardPerToken()` to update `StakingRewardsV2`:
```solidity
  function _updateReward(address _account) internal {
        rewardPerTokenStored = rewardPerToken();
        rewardPerTokenStoredUSDC = rewardPerTokenUSDC();
        lastUpdateTime = lastTimeRewardApplicable();

        if (_account != address(0)) {
            // update amount of rewards a user can claim
            rewards[_account] = earned(_account);

            // update reward per token staked AT this given time
            // (i.e. when this user is interacting with StakingRewards)
            userRewardPerTokenPaid[_account] = rewardPerTokenStored;

            rewardsUSDC[_account] = earnedUSDC(_account);

            userRewardPerTokenPaidUSDC[_account] = rewardPerTokenStoredUSDC;
        }
    }
```
This is the `rewardPerToken()` code in the `StakingRewardsV2`:
```solidity

    function rewardPerToken() public view returns (uint256) {
        uint256 allTokensStaked = totalSupply();

        if (allTokensStaked == 0) {
            return rewardPerTokenStored;
        }

        return rewardPerTokenStored
            + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / allTokensStaked);
    }
```
If for some period `totalSupply()` is 0, then the contract won't increase `rewardPerTokenStored`, and those period rewards will remain stuck in the contract.

The contract still has a [recoverERC20()](https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L710-L714) function to recover LP rewards. However, the contract still does not work as intended when `totalSupply()` is 0.

## Impact
Rewards can be stuck in the contract in the case of `totalSupply()` being 0.

## Code Snippet
https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L645-L666

https://github.com/sherlock-audit/2024-07-kwenta-staking-contracts/blob/0527fb7425206a3338c23177416436c6286cedf9/token/contracts/StakingRewardsV2.sol#L435-L444

## Tool used
Manual Review

## Recommendation
Add some mechanism to recalculate `rewardRate` or calculate undistributed rewards based on `rewardRate` when `totalSupply()` is 0.